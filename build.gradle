plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    id 'edu.sc.seis.launch4j' version '2.3.0'
}

group 'org.wdd'
version '1.0-SNAPSHOT'
mainClassName = "org.wdd.AccountGenerator"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

shadowJar {
    baseName = 'AccountGenerator'
    classifier = null
    version = null
}

launch4j {
    outputDir = "./dist/"
    outfile = 'AccountGenerator.exe'
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
}

task copyUnixRunner(type: Copy) {
    from "./AccountGeneartor.sh"
    into "build/dist/AccountGenerator.sh"
}

task app() {
    dependsOn createAllExecutables
    doLast {
        def fileSh = new File("$buildDir/dist/AccountGenerator.sh")
        fileSh.createNewFile()
        fileSh.text = """#!/bin/sh

java -jar `pwd`/lib/${project.tasks.shadowJar.archiveName} \$1
"""
        fileSh.setExecutable(true)
        
        def fileBat = new File("$buildDir/dist/AccountGenerator.bat")
        fileBat.createNewFile()
        fileBat.text = """

java -jar lib/${project.tasks.shadowJar.archiveName} %1
"""
    }
}

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.16'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.8.1'


    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.11'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0RC2'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.0RC2'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '2.5.1'
}
